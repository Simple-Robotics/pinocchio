#
# Copyright (c) 2015-2023 CNRS INRIA
# Copyright (c) 2015 Wandercraft, 86 rue de Paris 91400 Orsay, France.
#
# ----------------------------------------------------
# --- INCLUDE ----------------------------------------
# ----------------------------------------------------

# Extract the compile definitions of the project for export

REMOVE_PATH_FROM_LIST(${PROJECT_NAME}_CORE_SOURCES "src/" ${PROJECT_NAME}_CORE_SOURCES)
REMOVE_PATH_FROM_LIST(${PROJECT_NAME}_PARSERS_SOURCES "src/" ${PROJECT_NAME}_PARSERS_SOURCES)
REMOVE_PATH_FROM_LIST(${PROJECT_NAME}_COLLISION_SOURCES "src/" ${PROJECT_NAME}_COLLISION_SOURCES)
REMOVE_PATH_FROM_LIST(${PROJECT_NAME}_EXTRA_SOURCES "src/" ${PROJECT_NAME}_EXTRA_SOURCES)
PREPEND_PATH_FROM_LIST(${PROJECT_NAME}_CORE_PUBLIC_HEADERS
  "../"
  ${PROJECT_NAME}_CORE_PUBLIC_HEADERS)
PREPEND_PATH_FROM_LIST(${PROJECT_NAME}_PARALLEL_PUBLIC_HEADERS
  "../"
  ${PROJECT_NAME}_PARALLEL_PUBLIC_HEADERS)
PREPEND_PATH_FROM_LIST(${PROJECT_NAME}_COLLISION_PARALLEL_PUBLIC_HEADERS
  "../"
  ${PROJECT_NAME}_COLLISION_PARALLEL_PUBLIC_HEADERS)
PREPEND_PATH_FROM_LIST(${PROJECT_NAME}_PARSERS_PUBLIC_HEADERS
  "../"
  ${PROJECT_NAME}_PARSERS_PUBLIC_HEADERS)
PREPEND_PATH_FROM_LIST(${PROJECT_NAME}_COLLISION_PUBLIC_HEADERS
  "../"
  ${PROJECT_NAME}_COLLISION_PUBLIC_HEADERS)
PREPEND_PATH_FROM_LIST(${PROJECT_NAME}_EXTRA_PUBLIC_HEADERS
  "../"
  ${PROJECT_NAME}_EXTRA_PUBLIC_HEADERS)

# Create header-only target
#Â All other target will depend on it
ADD_LIBRARY(${PROJECT_NAME}_headers INTERFACE
  ${${PROJECT_NAME}_CORE_PUBLIC_HEADERS}
  ${${PROJECT_NAME}_CORE_GENERATED_PUBLIC_HEADERS})

# Enforce the preprocessed version of boost::list and boost::vector
# This information is redundant with the content of include/pinocchio/container/boost-container-limits.hpp
# but it avoids any compilation issue.
TARGET_COMPILE_DEFINITIONS(${PROJECT_NAME}_headers INTERFACE
  BOOST_MPL_LIMIT_LIST_SIZE=30
  BOOST_MPL_LIMIT_VECTOR_SIZE=30)

IF(INITIALIZE_WITH_NAN)
  TARGET_COMPILE_DEFINITIONS(${PROJECT_NAME}_headers INTERFACE
    EIGEN_INITIALIZE_MATRICES_BY_NAN)
ENDIF()

IF(CHECK_RUNTIME_MALLOC)
  TARGET_COMPILE_DEFINITIONS(${PROJECT_NAME}_headers INTERFACE
    PINOCCHIO_EIGEN_CHECK_MALLOC
    EIGEN_RUNTIME_NO_MALLOC)
ENDIF(CHECK_RUNTIME_MALLOC)

MODERNIZE_TARGET_LINK_LIBRARIES(${PROJECT_NAME}_headers SCOPE INTERFACE
  TARGETS Eigen3::Eigen
  INCLUDE_DIRS ${EIGEN3_INCLUDE_DIR})
MODERNIZE_TARGET_LINK_LIBRARIES(${PROJECT_NAME}_headers SCOPE INTERFACE
  TARGETS Boost::boost Boost::serialization
  LIBRARIES ${Boost_SERIALIZATION_LIBRARY}
  INCLUDE_DIRS ${Boost_INCLUDE_DIRS})

TARGET_INCLUDE_DIRECTORIES(${PROJECT_NAME}_headers INTERFACE
  $<INSTALL_INTERFACE:include>)

IF(MSVC)
  TARGET_COMPILE_OPTIONS(${PROJECT_NAME}_headers INTERFACE "/bigobj")
ENDIF()

IF(WIN32)
  TARGET_COMPILE_DEFINITIONS(${LIB_NAME} PUBLIC NOMINMAX)
ENDIF()

INSTALL(TARGETS ${PROJECT_NAME}_headers
  EXPORT ${TARGETS_EXPORT_NAME}
  LIBRARY DESTINATION ${CMAKE_INSTALL_FULL_LIBDIR}
  ARCHIVE DESTINATION ${CMAKE_INSTALL_FULL_LIBDIR}
  RUNTIME DESTINATION ${CMAKE_INSTALL_FULL_BINDIR})

# Define a compiled target
# This functions take sources and scalar type to use
FUNCTION(PINOCCHIO_TARGET target_name)
  SET(options INTERFACE)
  SET(oneValueArgs SCALAR LIBRARY_PUBLIC_SCOPE)
  SET(multiValueArgs SOURCES)
  CMAKE_PARSE_ARGUMENTS(ARGS "${options}" "${oneValueArgs}" "${multiValueArgs}"
                        ${ARGN})

  SET(LIB_NAME "${target_name}")

  # Manage different scope type if building an interface or shared library
  SET(LIBRARY_TYPE SHARED)
  SET(LIBRARY_PUBLIC_SCOPE PUBLIC)
  IF(ARGS_INTERFACE)
    SET(LIBRARY_TYPE INTERFACE)
    SET(LIBRARY_PUBLIC_SCOPE INTERFACE)
  ENDIF()

  # Export PUBLIC scope to caller
  IF(ARGS_LIBRARY_PUBLIC_SCOPE)
    SET(${ARGS_LIBRARY_PUBLIC_SCOPE} ${LIBRARY_PUBLIC_SCOPE} PARENT_SCOPE)
  ENDIF()

  ADD_LIBRARY(${LIB_NAME} ${LIBRARY_TYPE} ${ARGS_SOURCES})

  TARGET_LINK_LIBRARIES(${LIB_NAME} ${LIBRARY_PUBLIC_SCOPE} ${PROJECT_NAME}_headers)

  SET_TARGET_PROPERTIES(${LIB_NAME} PROPERTIES
    LINKER_LANGUAGE CXX
    INSTALL_RPATH "\$ORIGIN"
    VERSION ${PROJECT_VERSION})

  IF(ENABLE_TEMPLATE_INSTANTIATION AND NOT ARGS_INTERFACE)
    SET(PINOCCHIO_CONTEXT_FILE_VALUE "pinocchio/context/${ARGS_SCALAR}.hpp")
    TARGET_COMPILE_DEFINITIONS(${LIB_NAME}
      PUBLIC PINOCCHIO_ENABLE_TEMPLATE_INSTANTIATION
      PRIVATE PINOCCHIO_CONTEXT_FILE="${PINOCCHIO_CONTEXT_FILE_VALUE}")
  ENDIF()

  TARGET_INCLUDE_DIRECTORIES(${LIB_NAME} ${LIBRARY_PUBLIC_SCOPE} $<INSTALL_INTERFACE:include>)

  IF(BUILD_WITH_COMMIT_VERSION)
    TAG_LIBRARY_VERSION(${LIB_NAME})
  ENDIF()

  INSTALL(TARGETS ${LIB_NAME}
    EXPORT ${TARGETS_EXPORT_NAME}
    LIBRARY DESTINATION ${CMAKE_INSTALL_FULL_LIBDIR}
    ARCHIVE DESTINATION ${CMAKE_INSTALL_FULL_LIBDIR}
    RUNTIME DESTINATION ${CMAKE_INSTALL_FULL_BINDIR})
ENDFUNCTION()

# Define a template instantiation target
FUNCTION(PINOCCHIO_SPECIFIC_TYPE scalar_name scope)
  SET(LIB_NAME "${PROJECT_NAME}_${scalar_name}")
  IF(ENABLE_TEMPLATE_INSTANTIATION)
    PINOCCHIO_TARGET(${LIB_NAME}
      SCALAR ${scalar_name}
      SOURCES
        ${${PROJECT_NAME}_CORE_SOURCES}
      LIBRARY_PUBLIC_SCOPE PUBLIC_SCOPE)
    SET(${scope} ${PUBLIC_SCOPE} PARENT_SCOPE)
  ELSE()
    PINOCCHIO_TARGET(${LIB_NAME}
      SCALAR ${scalar_name}
      SOURCES
        ${${PROJECT_NAME}_CORE_SOURCES}
      LIBRARY_PUBLIC_SCOPE PUBLIC_SCOPE
      INTERFACE)
    SET(${scope} ${PUBLIC_SCOPE} PARENT_SCOPE)
  ENDIF()
ENDFUNCTION()

ADD_SOURCE_GROUP(${PROJECT_NAME}_CORE_SOURCES)
ADD_SOURCE_GROUP(${PROJECT_NAME}_PARSERS_SOURCES)
ADD_SOURCE_GROUP(${PROJECT_NAME}_EXTRA_SOURCES)
ADD_HEADER_GROUP(${PROJECT_NAME}_CORE_PUBLIC_HEADERS)
ADD_HEADER_GROUP(${PROJECT_NAME}_PARSERS_PUBLIC_HEADERS)
ADD_HEADER_GROUP(${PROJECT_NAME}_COLLISION_PUBLIC_HEADERS)
ADD_HEADER_GROUP(${PROJECT_NAME}_EXTRA_PUBLIC_HEADERS)
ADD_HEADER_GROUP(${PROJECT_NAME}_CORE_GENERATED_PUBLIC_HEADERS)

# Define the default target (double)
# This target will also have hpp-fcl and workspace module in it
PINOCCHIO_SPECIFIC_TYPE(default DEFAULT_SCOPE)

# Define the extra target
# This target hold extra algorithms
IF(BUILD_WITH_EXTRA_SUPPORT)
  SET(EXTRA_LIB_NAME "${PROJECT_NAME}_extra")

  PINOCCHIO_TARGET(${EXTRA_LIB_NAME}
    SCALAR default
    SOURCES
      ${${PROJECT_NAME}_EXTRA_SOURCES}
      ${${PROJECT_NAME}_EXTRA_PUBLIC_HEADERS})

  TARGET_LINK_LIBRARIES(${EXTRA_LIB_NAME} PUBLIC
    ${PROJECT_NAME}_default
    Qhull::qhullcpp
    Qhull::qhull_r)

  TARGET_COMPILE_DEFINITIONS(${EXTRA_LIB_NAME} PUBLIC
    PINOCCHIO_WITH_EXTRA_SUPPORT)
ENDIF()

# Define the parallel target
IF(BUILD_WITH_OPENMP_SUPPORT)
  SET(PARALLEL_LIB_NAME "${PROJECT_NAME}_parallel")

  PINOCCHIO_TARGET(${PARALLEL_LIB_NAME}
    SCALAR default
    SOURCES
      ${${PROJECT_NAME}_PARALLEL_PUBLIC_HEADERS}
    INTERFACE)

  TARGET_LINK_LIBRARIES(${PARALLEL_LIB_NAME} INTERFACE
                        ${PROJECT_NAME}_default OpenMP::OpenMP_CXX)
ENDIF()

# Define the collision target
IF(BUILD_WITH_HPP_FCL_SUPPORT)
  SET(COLLISION_LIB_NAME "${PROJECT_NAME}_collision")

  PINOCCHIO_TARGET(${COLLISION_LIB_NAME}
    SCALAR default
    SOURCES
      ${${PROJECT_NAME}_COLLISION_SOURCES}
      ${${PROJECT_NAME}_COLLISION_PUBLIC_HEADERS})

  TARGET_COMPILE_DEFINITIONS(${COLLISION_LIB_NAME} PUBLIC
    PINOCCHIO_WITH_HPP_FCL)
  TARGET_LINK_LIBRARIES(${COLLISION_LIB_NAME} PUBLIC
                        ${PROJECT_NAME}_default hpp-fcl::hpp-fcl)

  # Define the collision parallel target
  IF(BUILD_WITH_OPENMP_SUPPORT)
    SET(COLLISION_PARALLEL_LIB_NAME "${PROJECT_NAME}_collision_parallel")

    PINOCCHIO_TARGET(${COLLISION_PARALLEL_LIB_NAME}
      SCALAR default
      SOURCES
        ${${PROJECT_NAME}_COLLISION_PARALLEL_PUBLIC_HEADERS}
      INTERFACE)

    TARGET_LINK_LIBRARIES(${COLLISION_PARALLEL_LIB_NAME} INTERFACE
                          ${COLLISION_LIB_NAME} OpenMP::OpenMP_CXX)
  ENDIF()
ENDIF()

# Define the parsers target
# This target will have common tools for parsing/managing files and
# URDF/SRDF/SDF format support
IF(BUILD_WITH_PARSERS_SUPPORT)
  SET(PARSERS_LIB_NAME "${PROJECT_NAME}_parsers")

  PINOCCHIO_TARGET(${PARSERS_LIB_NAME}
    SCALAR default
    SOURCES
      ${${PROJECT_NAME}_PARSERS_SOURCES}
      ${${PROJECT_NAME}_PARSERS_PUBLIC_HEADERS})

  TARGET_LINK_LIBRARIES(${PARSERS_LIB_NAME} PUBLIC ${PROJECT_NAME}_default)
  IF(BUILD_WITH_HPP_FCL_SUPPORT)
    TARGET_LINK_LIBRARIES(${PARSERS_LIB_NAME} PUBLIC ${PROJECT_NAME}_collision)
  ENDIF()

  MODERNIZE_TARGET_LINK_LIBRARIES(${PARSERS_LIB_NAME} SCOPE PUBLIC
    TARGETS Boost::filesystem
    LIBRARIES ${Boost_FILESYSTEM_LIBRARY}}
    INCLUDE_DIRS ${Boost_INCLUDE_DIRS})

  # Special care of urdfdom version
  IF(BUILD_WITH_URDF_SUPPORT)
    TARGET_COMPILE_DEFINITIONS(${PARSERS_LIB_NAME} PUBLIC
      PINOCCHIO_WITH_URDFDOM)

    IF(${urdfdom_VERSION} VERSION_LESS "0.3.0")
      TARGET_COMPILE_DEFINITIONS(${PARSERS_LIB_NAME} PRIVATE
        PINOCCHIO_URDFDOM_COLLISION_WITH_GROUP_NAME)
    ENDIF()
    # defines types from version 0.4.0
    IF(NOT ${urdfdom_VERSION} VERSION_LESS "0.4.0")
      TARGET_COMPILE_DEFINITIONS(${PARSERS_LIB_NAME} PRIVATE
        PINOCCHIO_URDFDOM_TYPEDEF_SHARED_PTR)
    ENDIF()
    # std::shared_ptr appears from version 1.0.0
    IF(${urdfdom_VERSION} VERSION_GREATER "0.4.2")
      TARGET_COMPILE_DEFINITIONS(${PARSERS_LIB_NAME} PRIVATE
        PINOCCHIO_URDFDOM_USE_STD_SHARED_PTR)
    ENDIF()

    MODERNIZE_TARGET_LINK_LIBRARIES(${PARSERS_LIB_NAME} SCOPE PUBLIC
      TARGETS urdfdom::urdf_parser
      LIBRARIES ${urdfdom_LIBRARIES}
      INCLUDE_DIRS ${urdfdom_INCLUDE_DIRS})
  ENDIF()

  IF(BUILD_WITH_SDF_SUPPORT)
    TARGET_COMPILE_DEFINITIONS(${PARSERS_LIB_NAME} PUBLIC
      PINOCCHIO_WITH_SDFORMAT)

    TARGET_LINK_LIBRARIES(${PARSERS_LIB_NAME} PUBLIC ${SDFormat_LIBRARIES})
  ENDIF()
ENDIF()

# Define cppad codegen target
IF(BUILD_WITH_CODEGEN_SUPPORT)
  PINOCCHIO_SPECIFIC_TYPE(cppadcg CPPADCG_SCOPE)
  TARGET_INCLUDE_DIRECTORIES(${PROJECT_NAME}_cppadcg SYSTEM ${CPPADCG_SCOPE} ${cppadcg_INCLUDE_DIR})
  TARGET_LINK_LIBRARIES(${PROJECT_NAME}_cppadcg ${CPPADCG_SCOPE} ${cppadcg_LIBRARY} ${cppad_LIBRARY})
ENDIF()

# Define cppad target
IF(BUILD_WITH_AUTODIFF_SUPPORT)
  PINOCCHIO_SPECIFIC_TYPE(cppad CPPAD_SCOPE)
  TARGET_INCLUDE_DIRECTORIES(${PROJECT_NAME}_cppad SYSTEM ${CPPAD_SCOPE} ${cppad_INCLUDE_DIR})
  TARGET_LINK_LIBRARIES(${PROJECT_NAME}_cppad ${CPPAD_SCOPE} ${cppad_LIBRARY})
ENDIF()

# Define casadi target
IF(BUILD_WITH_CASADI_SUPPORT)
  PINOCCHIO_SPECIFIC_TYPE(casadi CASADI_SCOPE)
  TARGET_LINK_LIBRARIES(${PROJECT_NAME}_casadi ${CASADI_SCOPE} casadi)
ENDIF()

# Define main target (default, parsers, extra)
ADD_LIBRARY(${PROJECT_NAME} INTERFACE)
IF(ENABLE_TEMPLATE_INSTANTIATION)
  TARGET_LINK_LIBRARIES(${PROJECT_NAME} INTERFACE ${PROJECT_NAME}_default)
ENDIF()
IF(BUILD_WITH_PARSERS_SUPPORT)
  TARGET_LINK_LIBRARIES(${PROJECT_NAME} INTERFACE ${PROJECT_NAME}_parsers)
ENDIF()
IF(BUILD_WITH_OPENMP_SUPPORT)
  TARGET_LINK_LIBRARIES(${PROJECT_NAME} INTERFACE ${PROJECT_NAME}_parallel)
ENDIF()
IF(BUILD_WITH_HPP_FCL_SUPPORT)
  TARGET_LINK_LIBRARIES(${PROJECT_NAME} INTERFACE ${PROJECT_NAME}_collision)
  IF(BUILD_WITH_OPENMP_SUPPORT)
    TARGET_LINK_LIBRARIES(${PROJECT_NAME} INTERFACE ${PROJECT_NAME}_collision_parallel)
  ENDIF()
ENDIF()
IF(BUILD_WITH_EXTRA_SUPPORT)
  TARGET_LINK_LIBRARIES(${PROJECT_NAME} INTERFACE ${PROJECT_NAME}_extra)
ENDIF()

INSTALL(TARGETS ${PROJECT_NAME}
  EXPORT ${TARGETS_EXPORT_NAME}
  LIBRARY DESTINATION ${CMAKE_INSTALL_FULL_LIBDIR}
  ARCHIVE DESTINATION ${CMAKE_INSTALL_FULL_LIBDIR}
  RUNTIME DESTINATION ${CMAKE_INSTALL_FULL_BINDIR})
